(ns sdc.io-delay-test
  (:require [clojure.test :refer [deftest is testing]]
            [sdc.io-delay :as io-delay]))

(deftest io-delay-model
  (testing "no -max/-min/-rise/-fall -> applies to both corners and edges"
    (let [e (io-delay/input-delay {:port "data_in" :delay 2.0 :clock "CLK"})]
      (is (= :input (:direction e)))
      (is (true? (:max? e)))
      (is (true? (:min? e)))
      (is (true? (:rise? e)))
      (is (true? (:fall? e)))))
  (testing "explicit -max only narrows to that corner"
    (let [e (io-delay/input-delay {:port "data_in" :delay 2.0 :max? true})]
      (is (true? (:max? e)))
      (is (false? (:min? e)))))
  (testing "set_output_delay tags :direction :output"
    (is (= :output (:direction (io-delay/output-delay {:port "data_out" :delay 1.0}))))))

(deftest effective-delay-corner-resolution
  (testing "prefers the entry explicitly tagged for the requested corner"
    (let [entries [(io-delay/input-delay {:port "d" :delay 2.0 :max? true})
                   (io-delay/input-delay {:port "d" :delay 0.5 :min? true})]]
      (is (= 2.0 (io-delay/effective-delay entries :max)))
      (is (= 0.5 (io-delay/effective-delay entries :min)))))
  (testing "falls back to a generic (both-corners) entry"
    (let [entries [(io-delay/input-delay {:port "d" :delay 1.5})]]
      (is (= 1.5 (io-delay/effective-delay entries :max)))
      (is (= 1.5 (io-delay/effective-delay entries :min)))))
  (testing "a lone corner-specific entry is also used for the other corner"
    (let [entries [(io-delay/input-delay {:port "d" :delay 3.0 :max? true})]]
      (is (= 3.0 (io-delay/effective-delay entries :max)))
      (is (= 3.0 (io-delay/effective-delay entries :min))))))
